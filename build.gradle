plugins {
    id "org.jetbrains.kotlin.multiplatform" version "$kotlin_version"
    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlin_version"
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://kotlin.bintray.com/kotlinx' }
}

apply plugin: 'maven-publish'

publishing {
    repositories {
        maven {
            credentials {
                username System.getenv('BINTRAY_USER')
                password System.getenv('BINTRAY_API_KEY')
            }
            url maven_url
        }
    }
}

kotlin {
    jvm()
    js(BOTH) {
        browser {
            testTask {
                useMocha {
                    timeout = 10000
                }
            }
        }
        nodejs {
            testTask {
                useMocha {
                    timeout = 10000
                }
            }
        }
    }
    macosX64("macos")
    mingwX64("win64")
    linuxX64()

    ios {
        binaries {
            framework([DEBUG])
        }
    }
    watchos()
    tvos()

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "io.ktor:ktor-client-core:$ktor_version"
                implementation "io.ktor:ktor-client-json:$ktor_version"
                implementation "io.ktor:ktor-client-serialization:$ktor_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
                implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
                implementation "io.ktor:ktor-client-serialization-jvm:$ktor_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation "io.ktor:ktor-client-okhttp:$ktor_version"
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "io.ktor:ktor-client-core-js:$ktor_version"
                implementation "io.ktor:ktor-client-json-js:$ktor_version"
                implementation "io.ktor:ktor-client-serialization-js:$ktor_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-js')
                implementation "io.ktor:ktor-client-js:$ktor_version"
                implementation(npm("node-fetch", "*"))
                implementation(npm("text-encoding", "*"))
                implementation(npm("bufferutil", "*"))
                implementation(npm("utf-8-validate", "*"))
                implementation(npm("abort-controller", "*"))
                implementation(npm("fs", "*"))
            }
        }

        nativeCommonMain {
            dependsOn commonMain
            dependencies {
                implementation "io.ktor:ktor-client-core:$ktor_version"
                implementation "io.ktor:ktor-client-json:$ktor_version"
                implementation "io.ktor:ktor-client-serialization:$ktor_version"
            }
        }
        nativeCommonTest {
            dependsOn commonTest
        }
        desktopCommonMain {
            dependsOn nativeCommonMain
        }
        desktopCommonTest {
            dependsOn nativeCommonTest
            dependencies {
                implementation "io.ktor:ktor-client-curl:$ktor_version"
            }
        }

        configure([win64Main, macosMain, linuxX64Main]) {
            dependsOn desktopCommonMain
        }

        configure([win64Test, macosTest, linuxX64Test]) {
            dependsOn desktopCommonTest
        }

        iosMain {
            dependsOn nativeCommonMain
        }
        iosTest {
            dependsOn nativeCommonTest
            dependencies {
                implementation "io.ktor:ktor-client-ios:$ktor_version"
            }
        }

        // Configure tvos and watchos to build on ios sources
        configure([tvosMain, watchosMain]) {
            dependsOn iosMain
        }
        configure([tvosTest, watchosTest]) {
            dependsOn iosTest
            kotlin.srcDirs += "src/iosTest/kotlin"
        }

        // Added for use in the swift demo
        iosX64Main {
            dependencies {
                implementation "io.ktor:ktor-client-ios:$ktor_version"
            }
        }
    }
}
